# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=vmware-ovftool
pkgver=4.4.3
pkgrel=18663434
_pkgver="${pkgver}-${pkgrel}"
pkgdesc="VMware Open Virtualization Format tool"
arch=('x86_64')
url="https://www.vmware.com/support/developer/ovf/"
license=('custom:vmware')
makedepends=('xorg-server-xvfb' 'procps-ng')
depends=('c-ares' 'expat' 'zlib' 'libxcrypt-compat')
# VMware-ovftool must be provided by the user by putting it into the build directory
# You can get the file by yourself here:
# https://my.vmware.com/group/vmware/downloads/get-download?downloadGroup=OVFTOOL443
source=("file://VMware-ovftool-${_pkgver}-lin.${CARCH}.bundle")
sha256sums=('8d69c6057d47f7060b1ce3dd90fcb2b688a02034d39761ad301620bb9a6b1c04')

prepare() {
  # The bundle file doesn't allow extraction using symlinks or relative paths, here's then copied
  _bundlepath=$(readlink "${srcdir}/VMware-ovftool-${_pkgver}-lin.${CARCH}.bundle")
  cp -f -L "${_bundlepath}" "${srcdir}/VMware-ovftool-${_pkgver}-${CARCH}_file.bundle"
}

build() {
  rm -rf "build"
  xvfb-run -a sh "VMware-ovftool-${_pkgver}-${CARCH}_file.bundle" -x "${srcdir}/build"
}

package() {
  cd "build/${pkgname}"
  # Install binaries files
  install -m 755 -d "${pkgdir}/usr/lib/${pkgname}"
  install -m 755 -t "${pkgdir}/usr/lib/${pkgname}" ovftool ovftool.bin lib*
  install -m 644 -t "${pkgdir}/usr/lib/${pkgname}" icudt44l.dat
  # Install data files
  for _subdir in "certs" "env" "env/en" "schemas/DMTF" "schemas/vmware"
  do
    install -m 755 -d "${pkgdir}/usr/lib/${pkgname}/${_subdir}"
    install -m 644 -t "${pkgdir}/usr/lib/${pkgname}/${_subdir}" "${_subdir}"/*.*
  done
  # Install main script symlink
  install -m 755 -d "${pkgdir}/usr/bin"
  ln -s "/usr/lib/${pkgname}/ovftool" "${pkgdir}/usr/bin/ovftool"
  # Install license files
  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" \
    "vmware.eula" "vmware-eula.rtf" "open_source_licenses.txt"
  # Install documentation files
  install -m 755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/doc/${pkgname}" "README.txt"
}

